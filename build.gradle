buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://repo.enonic.com/public'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.enonic.xp:gradle-plugin:${xpVersion}"
        classpath "com.moowork.gradle:gradle-node-plugin:0.11"
    }
}

apply plugin: 'com.enonic.xp.app'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.moowork.node'

app {
    name = project.appName
    displayName = project.displayName
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
}



dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
    include "com.enonic.xp:lib-xslt:${xpVersion}"
    include "com.enonic.xp:lib-mustache:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"

    include "com.enonic.lib:util:1.0.0"
    include "com.enonic.lib:menu:1.1.1"

    testCompile 'junit:junit:4.10'
    testCompile "com.enonic.xp:testing:${xpVersion}"
}

node{
    version '4.2.1'
    download true
}

task babelJs(type:NodeTask, dependsOn:'npmInstall'){
    script = file('node_modules/babel-cli/lib/babel')
    args = [
            "-d",
            "$buildDir/resources/main",
            "-x",
            ".es6",
            "--presets",
            "es2015",
            "src/main/resources"
    ]
    inputs.dir "src/main/resources"
    outputs.dir "$buildDir/resources/main"
}

task babelJsTest(type:NodeTask, dependsOn:'npmInstall'){
    script = file('node_modules/babel-cli/lib/babel')
    args = [
            "-d",
            "$buildDir/resources/test",
            "-x",
            ".es6",
            "--presets",
            "es2015",
            "src/test/resources"
    ]
    inputs.dir "src/test/resources"
    outputs.dir "$buildDir/resources/test"
}

task webpack(type:NodeTask, dependsOn:'npmInstall') {
    script = file('node_modules/webpack/bin/webpack.js')
    args = [
            '--config webpack.config.js'
    ]
}

webpack.mustRunAfter(babelJs, babelJsTest)

test.dependsOn += babelJs
test.dependsOn += babelJsTest
test.dependsOn += webpack


jar.dependsOn += babelJs
jar.dependsOn += babelJsTest
jar.dependsOn += webpack

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://repo.enonic.com/public'
    }
}
